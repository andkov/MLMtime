TITLE:
  Eq 2.15: Adding Sex by Dementia Interaction
  Sex (0=Women), Dementia (0=Future)

DATA:
  FILE = MPLUS_Chapter2.csv;
  FORMAT = FREE;

DEFINE:
  age85 = age - 85;
  grip9 = grip - 9;
  IF sexMW EQ 0 THEN sexWM=1;
  IF sexMW EQ 1 THEN sexWM=0;
  IF demgroup EQ 1 THEN demFN=1;
  IF demgroup EQ 1 THEN demFC=0;
  IF demgroup EQ 2 THEN demFN=0;
  IF demgroup EQ 2 THEN demFC=0;
  IF demgroup EQ 3 THEN demFN=0;
  IF demgroup EQ 3 THEN demFC=1;
  agegrip = age85*grip9;
  sexFN = sexWM*demFN;
  sexFC = sexWM*demFC;

VARIABLE:
  NAMES = PersonID cog age grip sexMW demgroup;
  USEVARIABLES = cog age85 grip9 sexWM demFN demFC agegrip sexFN sexFC;
  MISSING = ALL (-9999);

ANALYSIS:
  ESTIMATOR = ML;
  PROCESSORS = 4;

MODEL:
  cog ON age85 grip9 sexWM (bage85 bgrip9 bsexWM);
  cog ON demFN demFC (bdemFN bdemFC);
  cog ON agegrip sexFN sexFC (bagegrip bsexFN bsexFC);
